;イメージビュアーで自動スライドショー
;指定した間隔で自動スライドショーを行います。
;---------------------------------------------------------------------------

;イメージビュアーでなければ中断
Test_ScrMode==IV: IfFalse_Exit

;間隔 (必須)
Set_間隔=10S

;ループの繰り返し回数
;  無指定だとエンドレスになります。
;
Set_繰り返し回数=

;実行時間または終了時刻を指定
;  無指定だと無視されます。
;  時刻の場合はダブルクォーテーションで囲んでください。
;  例: 90分後に終了
;    Timer_90M
;  例: 12:00:00に終了
;    Timer_"12:00:00"
;
Timer_30M

;バッテリー低下時に終了
;  0 だと無視されます
Set_バッテリー下限値=0

;--------------------------------------
;サイドバー非表示
Sidebar_OFF

;サムネイル非表示
Thumbnail_OFF

;全画面表示
FullScreen_ON

;---------------------------------------------------------------------------
;スライドショー開始

TopFile
Repeat
  ;タイマーチェック
  IfTimeOut_Break

  ;バッテリーチェック
  Test_バッテリー下限値>0
  IfTrue
    Test_Battery<=%バッテリー下限値%
    IfTrue_Break
  EndIf

  Sleep_%間隔%

  ;次ファイルへ
  IfNotEnd
    NextFile
  ;最後のファイル
  Else
    ;繰り返しの回数のチェック
    Test_繰り返し回数>0
    IfTrue
      Set_繰り返し回数-=1
      Test_繰り返し回数==0
      IfTrue_Break
    Else
      TopFile
    EndIf
  EndIf
EndRepeat

;通常表示に戻す
FullScreen_OFF
